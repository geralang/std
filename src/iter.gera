
mod std::iter

use std::opt

pub proc map(iter, mapping) = || {
    case iter() {
        #next n -> return #next mapping(n)
        #end -> return #end unit
    }
}

pub proc filter(iter, condition) = || {
    mut result = #end unit
    exhaust(|| {
        val current
        case iter() {
            #next n -> current = n
            #end -> return #end unit
        }
        case condition(current) -> {
            result = #next current
            return #end unit
        }
        return #next unit
    })
    return result
}

pub proc reduce(iter, reduction, initial) {
    mut acc = initial
    iter |> map(-> {
        acc = reduction(acc, it)
    }) |> exhaust()
    return acc
}

pub proc take(iter, n) {
    mut i = 0
    return || {
        case i >= n -> return #end unit
        i = i + 1
        return iter()
    }
}

pub proc take_while(iter, cond) {
    mut done = false
    return || {
        case done -> return #end unit
        val value
        case iter() {
            #next n -> value = n
            #end -> return #end unit
        }
        case cond(value) -> return #next value
        done = true
        return #end unit
    }
}

pub proc skip(iter, n) {
    iter |> take(n) |> exhaust()
    return iter
}

pub proc for_each(iter, action) = iter
    |> map(action)
    |> exhaust()

pub proc zip(iter_a, iter_b, combinator) = || {
    val val_a
    case iter_a() {
        #next v -> val_a = v
        #end -> return #end unit
    }
    val val_b
    case iter_b() {
        #next v -> val_b = v
        #end -> return #end unit
    }
    return #next combinator(val_a, val_b)
}

pub proc chain(iter_a, iter_b) = || {
    case iter_a() {
        #next n -> return #next n
        #end -> return iter_b()
    }
}

pub proc count(iter) = iter
    |> reduce(|count, e| count + 1, 0)

pub proc next(iter) {
    case iter() {
        #next v -> return #some v
        #end -> return #none unit
    }
}

pub proc last(iter) {
    mut result = #none unit
    iter |> for_each(|v| {
        result = #some v
    })
    return result
}

pub proc find(iter, condition) = iter
    |> filter(condition)
    |> next()

pub proc find_last(iter, condition) = iter
    |> filter(condition)
    |> last()

pub proc enumerate(iter) {
    mut i = 0
    return iter |> map(|v| {
        val ci = i
        i = i + 1
        return { idx = ci, val = v }
    })
}

pub proc has(iter, element) = iter
    |> find(|e| e == element)
    |> opt::is_some()

pub proc empty() = || #end unit

pub proc over_value(v) {
    mut done = false
    return || {
        case done -> return #end unit
        done = true
        return #next v
    }
}

pub proc repeat_over(f) = || #next f()