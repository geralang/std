
mod std::iter

use std::opt

pub proc map(iter, mapping) {
    return || {
        case iter() {
            #end -> return #end unit
            #next n -> return #next mapping(n)
        }
    }
}

pub proc filter(iter, condition) {
    return || {
        var result = { val = #end unit }
        exhaust(|| {
            var current
            case iter() {
                #end -> return #end unit
                #next n -> current = n
            }
            case condition(current) -> {
                result.val = #next current
                return #end unit
            }
            return #next unit
        })
        return result.val
    }
}

pub proc reduce(iter, reduction, initial) {
    var value = { val = initial }
    iter |> map(|v| {
        value.val = reduction(value.val, v)
    }) |> exhaust()
    return value.val
}

pub proc take(iter, n) {
    var taken = { count = 0 }
    return || {
        case taken.count >= n -> return #end unit
        taken.count = taken.count + 1
        return iter()
    }
}

pub proc take_while(iter, cond) {
    mut var done = false
    return || {
        case done -> return #end unit
        var value
        case iter() {
            #end -> return #end unit
            #next n -> value = n
        }
        case cond(value) -> return #next value
        done = true
        return #end unit
    }
}

pub proc skip(iter, n) {
    iter |> take(n) |> exhaust()
    return iter
}

pub proc for_each(iter, action) {
    iter |> map(action) |> exhaust()
}

pub proc zip(iter_a, iter_b, combinator) {
    return || {
        var val_a
        case iter_a() {
            #end -> return #end unit
            #next v -> val_a = v
        }
        var val_b
        case iter_b() {
            #end -> return #end unit
            #next v -> val_b = v
        }
        return #next combinator(val_a, val_b)
    }
}

pub proc chain(iter_a, iter_b) {
    return || {
        case iter_a() {
            #next n -> return #next n
            #end -> {}
        }
        return iter_b()
    }
}

pub proc count(iter) {
    return iter
        |> reduce(|count, e| count + 1, 0)
}

pub proc next(iter) {
    case iter() {
        #end -> return #none unit
        #next v -> return #some v
    }
}

pub proc last(iter) {
    var result = { value = #none unit }
    iter |> for_each(|v| {
        result.value = #some v
    })
    return result.value
}

pub proc from_value(value) {
    mut var done = false
    return || {
        case done -> return #end unit
        done = true
        return #next value
    }
}

pub proc find(iter, condition) {
    return iter |> filter(condition) |> next()
}

pub proc find_last(iter, condition) {
    return iter |> filter(condition) |> last()
}

pub proc enumerate(iter) {
    mut var i = 0
    return iter |> map(|v| {
        var ci = i
        i = i + 1
        return { idx = ci, val = v }
    })
}

pub proc has(iter, element) {
    return iter
        |> find(|e| e == element)
        |> opt::is_some()
}

pub proc empty() {
    return || #end unit
}

pub proc over_value(v) {
    mut var done = false
    return || {
        case done -> return #end unit
        done = true
        return #next v
    }
}

pub proc repeat_over(f) {
    return || {
        return #next f()
    }
}