/// Provides a simple hash map implementation.
mod std::coll::HashMap

use std::math::abs
use std::iter::*
use std::opt::*

proc choose_bucket(key_hash, capacity) = abs(key_hash % capacity)

proc iter_nodes(start_node) {
    mut current_node = start_node
    return || {
        case current_node {
            #some node -> {
                current_node = node.next
                return #next node
            }
        } else return #end unit
    }
}

val INITIAL_SIZE = 16

/// Creates and returns a new hash map with an intitial capacity of 16.
pub proc new() = with_capacity(INITIAL_SIZE)

proc with_capacity(initial_capacity) = {
    hasher = |v| hash(v),
    buckets = [#none unit; initial_capacity],
    load_factor = 0.75,
    size = 0,

    grow = |self, capacity| {
        case length(self.buckets) >= capacity -> return self
        val old_values = self .> iter()
        self.buckets = [#none unit; capacity]
        old_values |> for_each(|v|
            self .> set(v.key, v.value)
        )
        return self
    },

    set = |self, key, value| {
        val key_hash = key |> self.hasher()
        val bucket_idx = key_hash |> choose_bucket(length(self.buckets))
        case self.buckets[bucket_idx]
            |> iter_nodes()
            |> find(|node| node.key_hash == key_hash) {
            #some node -> {
                node.key = key
                node.value = value
            }
            #none -> {
                val first_node = self.buckets[bucket_idx]
                self.buckets[bucket_idx] = #some {
                    key_hash = key_hash,
                    key = key,
                    value = value,
                    next = first_node
                }
                self.size = self.size + 1
                val max_size = as_flt(length(self.buckets)) * self.load_factor
                    |> as_int()
                case self.size > max_size
                    -> self .> grow(length(self.buckets) * 2)
            }
        }
        return self
    },

    at = |self, key| {
        val key_hash = key |> self.hasher()
        val bucket_idx = key_hash |> choose_bucket(length(self.buckets))
        return self.buckets[bucket_idx]
            |> iter_nodes()
            |> find(|node| node.key_hash == key_hash)
            |> map(|node| node.value)
    },

    has = |self, key| self .> at(key)
        |> is_some(),

    remove = |self, key| {
        val key_hash = key |> self.hasher()
        val bucket_idx = key_hash |> choose_bucket(length(self.buckets))
        return self.buckets[bucket_idx]
            |> iter_nodes()
            |> find(|node| node.key_hash == key_hash)
            |> map(|node| {
                self.buckets[bucket_idx] = node.next
                self.size = self.size - 1
                return node.value
            })
    },

    iter = |self| {
        val buckets = self.buckets
        mut bucket_idx = 0
        mut current_node = #none unit
        return || {
            val node
            case current_node {
                #some n -> node = n
            } else {
                bucket_idx = (bucket_idx + 1)..length(buckets)
                    |> find(|i| buckets[i] |> is_some())
                    |> unwrap_or(length(buckets))
                case bucket_idx >= length(buckets)
                    -> return #end unit
                node = buckets[bucket_idx]
                    |> expect("Just checked above!")
            }
            val entry = { key = node.key, value = node.value }
            current_node = node.next
            return #next entry
        }
    },

    iter_keys = |self| self
        .> iter()
        |> map(|v| v.key),

    iter_values = |self| self
        .> iter()
        |> map(|v| v.value),

    clone = |self| self
        .> iter()
        |> collect()

}

/// Creates and returns a new hash map and collects all `key` - `value`
/// pair objects from the given iterator `from_iter` into it.
pub proc collect(from_iter) {
    val dest = new()
    from_iter |> for_each(|v| dest .> set(v.key, v.value))
    return dest
}