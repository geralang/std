
mod std::str

use std::(iter, opt, math, res)

pub proc substring(s, start_idx, end_idx) {
    return core::substring(s, start_idx, end_idx)
}

pub proc substring_until(s, end_index) {
    return s |> substring(0, end_index)
}

pub proc substring_after(s, start_index) {
    return s |> substring(start_index, s |> length())
}

pub proc at(s, index) {
    return s |> substring(index, index + 1)
}

pub proc starts_with(s, prefix) {
    case length(prefix) > length(s) -> return false
    var compared_prefix = s
        |> substring_until(length(prefix))
    return compared_prefix == prefix
}

pub proc ends_with(s, suffix) {
    case length(suffix) > length(s) -> return false
    var compared_suffix = s
        |> substring_after(-length(suffix))
    return compared_suffix == suffix
}

pub proc find(s, sub) {
    var current = { index = 0 }
    return || {
        mut var found = { value = #end unit }
        exhaust(|| {
            case current.index > length(s) - length(sub) -> return #end unit
            var c = current.index
            current.index = current.index + 1
            var compared_sub = s |> substring(c, c + length(sub))
            case compared_sub == sub -> {
                found.value = #next c
                return #end unit
            }
            return #next unit
        })
        return found.value
    }
}

pub proc pad_left(s, target_length, padding) {
    var fill_length = target_length - length(s)
    return padding
        |> string(math::max(fill_length / length(padding), 0))
        |> concat(padding |> substring_until(fill_length % length(padding)))
        |> concat(s)
}

pub proc pad_right(s, target_length, padding) {
    var fill_length = target_length - length(s)
    return s
        |> concat(padding |> string(math::max(fill_length / length(padding), 0)))
        |> concat(padding |> substring_until(fill_length % length(padding)))
}

pub proc fmt(s, v) {
    var current = { ci = 0, vi = 0, rs = s }
    exhaust(|| {
        case current.ci >= length(current.rs) -> return #end unit
        case current.vi >= length(v) -> return #end unit
        var ci = current.ci
        case at(current.rs, ci) == "_" -> {
            var inserted = v[current.vi]
            current.rs = current.rs
                |> substring_until(ci)
                |> concat(inserted)
                |> concat(current.rs |> substring_after(ci + 1))
            current.ci = ci + length(inserted)
            current.vi = current.vi + 1
        } else {
            current.ci = ci + 1
        }
        return #next unit
    })
    case current.vi < length(v) -> {
        "the format string '_' does not \
contain enough underscores for _ value(s)"
            |> fmt([s, v |> length() |> as_str()])
            |> panic()
    }
    return current.rs
}

pub proc split(s, separator) {
    var current = {
        anchor = 0,
        done = false
    }
    var until_length = length(s) - length(separator)
    return || {
        case current.done -> return #end unit
        var next_split
        case current.anchor <= until_length
            -> next_split = current.anchor..=until_length
                |> iter::find(|index| 
                    substring(s, index, index + length(separator)) == separator
                ) 
        else next_split = #none unit
        case next_split {
            #none -> {
                var result = s |> substring_after(
                    math::min(current.anchor, length(s))
                )
                current.anchor = length(s)
                current.done = true
                return #next result
            }
            #some i -> {
                var result = s |> substring(current.anchor, i)
                current.anchor = i + length(separator)
                return #next result
            }
        }
    }
}

pub proc join(strings, separator) {
    mut var result = {
        value = "",
        had_part = false
    }
    strings |> iter::for_each(|s| {
        case result.had_part
            -> result.value = result.value |> concat(separator)
        result.value = result.value |> concat(s)
        result.had_part = true
    })
    return result.value
}

pub proc iter(s) {
    return 0..length(s)
        |> iter::map(|i| s |> at(i))
}

proc parse_digit(c) {
    var d
    case c {
        "0" -> d = 0  "1" -> d = 1  "2" -> d = 2
        "3" -> d = 3  "4" -> d = 4  "5" -> d = 5
        "6" -> d = 6  "7" -> d = 7  "8" -> d = 8
        "9" -> d = 9
    } else return #err unit
    return #ok d
}

proc parse_digits(s) {
    return s
        |> iter()
        |> iter::reduce(|r, c| r |> res::and_then(|v|
            parse_digit(c)
                |> res::map(|d| v * 10 + d)
        ), #ok 0)
        |> res::map(|r| #some r)
        |> res::unwrap_or(#none unit)
}

pub proc parse_int(source) {
    case length(source) == 0 -> return #none unit
    mut var start = 0
    mut var sign = 1
    case source |> at(0) {
        "+" -> {
            case length(source) == 1 -> return #none unit
            start = 1
        }
        "-" -> {
            case length(source) == 1 -> return #none unit
            start = 1
            sign = -1
        }
    }
    return source
        |> substring_after(start)
        |> parse_digits()
        |> opt::map(|r| r * sign)
}

proc char_to_ascii_lowercase(c) {
    var uppercase = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
    var lowercase = "abcdefghijklmnopqrstuvwxyz"
    return uppercase
        |> find(c)
        |> iter::next()
        |> opt::map(|i| lowercase |> at(i))
        |> opt::unwrap_or(c)
}

proc to_ascii_lowercase(s) {
    return s
        |> iter()
        |> iter::map(char_to_ascii_lowercase)
        |> join("")
}

proc parse_fractional(source) {
    // parses '<int>(.<int>)' (parens = optional)
    case length(source) == 0 -> return #none unit
    var dot = source
        |> find(".")
        |> iter::next()
        |> opt::unwrap_or(source |> length())
    var whole
    case source
        |> substring_until(dot)
        |> parse_int() {
        #some r -> whole = r |> as_flt()
        #none -> case dot == 0 -> whole = 0.0
            else return #none unit
    }
    case dot >= length(source) -> return #some whole
    return 1..(length(source) - dot)
        |> iter::reduce(|r, i| r |> res::and_then(|v| source
            |> at(dot + i)
            |> parse_digit()
            |> res::map(|d| v + as_flt(d) * math::pow(10.0, -as_flt(i)))
        ), #ok 0.0)
        |> res::map(|fraction| #some whole + fraction)
        |> res::unwrap_or(#none unit)
}

pub proc parse_flt(source) {
    case length(source) == 0 -> return #none unit
    // (optional) '+' or '-'
    mut var start = 0
    mut var sign = 1.0
    case source |> at(0) {
        "+" -> {
            case length(source) == 1 -> return #none unit
            start = 1
        }
        "-" -> {
            case length(source) == 1 -> return #none unit
            start = 1
            sign = -1.0
        }
    }
    var after_sign = source
        |> substring_after(start)
    // rest of input may be 'nan', 'inf' or 'infinity' here (case-insensitive)
    case after_sign |> to_ascii_lowercase() {
        "nan" -> return #some math::NAN * sign
        "inf" -> return #some math::INF * sign
        "infinity" -> return #some math::INF * sign
    }
    // parse everything up to the next 'e' or 'E' as a fractional
    var exponent = 0..length(after_sign)
        |> iter::find(|i| at(after_sign, i) == "e" || at(after_sign, i) == "E")
        |> opt::unwrap_or(after_sign |> length())
    var fractional
    case after_sign
        |> substring_until(exponent)
        |> parse_fractional() {
        #none -> return #none unit
        #some f -> fractional = f
    }
    // exponent is optional
    case exponent >= length(after_sign) -> return #some sign * fractional
    // sign of exponent after 'e' or 'E' (optional) '+' or '-'
    mut var exponent_sign = 1.0
    mut var exponent_start = exponent + 1
    case exponent_start >= length(after_sign) -> return #some sign * fractional
    case after_sign |> at(exponent_start) {
        "+" -> exponent_start = exponent + 2
        "-" -> {
            exponent_start = exponent + 2
            exponent_sign = -1.0
        }
    }
    // exponent value
    var exponent_val
    case after_sign
        |> substring_after(exponent_start)
        |> parse_int() {
        #some e -> exponent_val = math::pow(10.0, as_flt(e) * exponent_sign)
        #none -> case exponent_start == length(after_sign) -> exponent_val = 1.0
            else return #none unit
    }
    return #some sign * fractional * exponent_val
} 

pub proc repeat(repeated, times) {
    return iter::repeat_over(|| repeated)
        |> iter::take(times)
        |> join("")
}